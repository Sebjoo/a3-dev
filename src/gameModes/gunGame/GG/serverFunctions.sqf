if (isnil "GG_var_coverMap") then {
    GG_var_coverMap = objNull;
};

GG_var_mapData = [
    [[9095.6, 12014.8, 0], 0, 250, [9096.34, 12014.6, 0],[160, 160], 309.627, "ELLIPSE"],
    [[9248.83, 15861.9, 0], 0, 250, [9249.64, 15861.6, 0],[140, 220], 253.301, "ELLIPSE"],
    [[16481.4, 17218.7, 0], 0, 250, [16481.6, 17217.5, 0],[200, 160], 177.233, "ELLIPSE"],
    [[4020.08, 12850.5, -2.54261], 0, 350, [4020.89, 12850.9, 0],[250, 250], 165.503, "ELLIPSE"],
    [[21353.7, 16352.8, 0], 0, 250, [21353.3, 16352.2, 0],[220, 140], 334.49, "ELLIPSE"],
    [[20536.4, 8865.02, 0.000480652], 0, 200, [20535.6, 8864.48, 0],[140, 170], 255.175, "ELLIPSE"],
    [[20802.3, 6719.5, 0], 0, 200, [20802.3, 6718.58, 0],[120, 150], 341.38, "ELLIPSE"],
    [[21839.7, 20978.3, 0], 0, 300, [21839.4, 20978.5, 0],[200, 130], 172.94, "ELLIPSE"],
    [[16638.6, 16115.6, 0], 0, 250, [16638.4, 16115.9, 0],[200, 140], 309.627, "ELLIPSE"],
    [[7099.41,16455.3,0],0,300,[7099.47,16455.3,0],[260,230],136.569,"ELLIPSE"],
    [[4915.52,16186.4,0],0,250,[4915.58,16187.5,0],[200,150],187.187,"ELLIPSE"],
    [[4995.89,11176.5,0],0,550,[4996.26,11176,0],[500,400],118.203,"ELLIPSE"],
    [[10708.8,12143.8,0],0,450,[10704.7,12141.7,0],[430,300],63.35,"ELLIPSE"],
    [[10982.9,13389.2,0],0,350,[10983.2,13389.2,0],[330,330],63.35,"ELLIPSE"],
    [[11752.6,13703.2,0],0,180,[11753.3,13703.8,0],[170,170],63.35,"ELLIPSE"],
    [[11405.3,14214,0],0,200,[11405.1,14214.1,0],[140,125],304.416,"RECTANGLE"],
    [[11118.7,14548.9,0],0,200,[11119.2,14549.2,0],[170,170],304.416,"ELLIPSE"],
    [[12506.4,14380.5,0],0,390,[12506.4,14379,0],[380,270],163.47,"ELLIPSE"],
    [[12915.2,15056.8,0],0,250,[12915.2,15057.7,0],[240,240],163.47,"ELLIPSE"],
    [[12404.4,15728.4,6.62518],0,300,[12404.3,15727.3,0],[280,280],99.159,"ELLIPSE"],
    [[12603.7,16410.6,6.62496],0,220,[12603.9,16411.3,0],[230,200],52.15,"ELLIPSE"],
    [[8541.51,18283.7,6.62521],0,300,[8541.73,18283.7,0],[280,220],174.807,"ELLIPSE"],
    [[10322.7,19088,6.62505],0,180,[10322.1,19088.1,0],[170,170],174.807,"ELLIPSE"],
    [[9452.61,20248.2,6.62486],0,180,[9452,20248.3,0],[170,170],174.807,"ELLIPSE"],
    [[3931,17492.8,109.188],0,420,[3930.22,17495.3,0],[420,200],242.334,"ELLIPSE"],
    [[4588.9,21410.1,0],0,145,[4588.86,21410.4,0],[140,130],185.323,"ELLIPSE"],
    [[14693.7,20776.6,62.647],0,270,[14694.4,20777,0],[270,180],202.16,"ELLIPSE"],
    [[14044.4,18696,0],0,260,[14044.2,18696.2,0],[250,250],85.921,"ELLIPSE"],
    [[14462.1,17659.5,0.000463486],0,210,[14461.9,17661.9,0],[200,200],85.921,"ELLIPSE"],
    [[15379.1,16002.2,15.8233],0,210,[15380.3,16001.3,0],[130,170],177.457,"RECTANGLE"],
    [[18769.6,16606.5,0],0,280,[18770.7,16608.3,0],[270,210],86.5096,"ELLIPSE"],
    [[18195.5,15289.1,0],0,400,[18197.8,15287.3,0],[410,300],142.023,"ELLIPSE"],
    [[16830.8,12697.2,1.90735e-006],0,400,[16833.1,12695.4,0],[410,300],142.023,"ELLIPSE"],
    [[19390.4,13264.8,0],0,190,[19390.5,13265.5,0],[180,180],156.852,"ELLIPSE"],
    [[20212.5,11708.9,0.0240479],0,250,[20212.3,11709.1,0],[250,210],156.852,"ELLIPSE"],
    [[21691.1,7602.31,27.321],0,220,[21690.7,7602.34,0],[220,190],246.1,"ELLIPSE"],
    [[20969.7,16996.7,3.29211],0,270,[20969.8,16997.5,0],[260,260],159.788,"ELLIPSE"],
    [[20952,19250.9,3.29201],0,150,[20951,19249.6,0],[110,110],109.255,"RECTANGLE"],
    [[25638.4,21317.6,2.245],0,320,[25639.6,21317.7,0],[320,240],152.524,"ELLIPSE"],
    [[27010.6,23205.3,2.245],0,280,[27009.8,23204.9,0],[270,200],152.524,"ELLIPSE"],
    [[4127.34,11753.5,0],0,210,[4127.11,11754.6,0],[200,200],152.524,"ELLIPSE"],
    [[3696.76,11651.1,0],0,350,[3696.75,11651.4,0],[360,280],243.337,"ELLIPSE"],
    [[3558.76,13020.9,3.24249e-005],0,500,[3560.93,13021.8,0],[500,320],99.708,"ELLIPSE"],
    [[3831.27,13683.5,1.0091],0,310,[3830.73,13683.3,0],[300,300],99.45,"ELLIPSE"]
];

GG_var_weaponList = [
    ["MMG_01_hex_F", "150Rnd_93x64_Mag", "optic_AMS_khk"],
    ["MMG_02_sand_F", "130Rnd_338_Mag", "optic_AMS_khk"],
    ["LMG_Zafir_F", "150Rnd_762x54_Box_Tracer", "optic_MRCO"],
    ["arifle_MX_SW_F", "100Rnd_65x39_caseless_mag", "optic_Hamr"],
    ["LMG_Mk200_F", "200Rnd_65x39_cased_Box_Tracer", "optic_Arco"],
    ["LMG_03_F", "200Rnd_556x45_Box_Tracer_F", "optic_Hamr"],
    ["arifle_CTARS_hex_F", "100Rnd_580x42_hex_Mag_F", "optic_Arco_blk_F"],
    ["arifle_RPK12_F", "75rnd_762x39_AK12_Mag_F", "optic_Arco"],
    ["arifle_SPAR_02_khk_F", "150Rnd_556x45_Drum_Mag_Tracer_F", "optic_Arco"],
    ["srifle_DMR_04_Tan_F", "10Rnd_127x54_Mag", "optic_ACO_grn_smg"],
    ["srifle_GM6_F", "5Rnd_127x108_Mag", "optic_SOS"],
    ["srifle_LRR_F", "7Rnd_408_Mag", "optic_SOS"],
    ["srifle_DMR_05_hex_F", "10Rnd_93x64_DMR_05_Mag", "optic_AMS_khk"],
    ["srifle_DMR_02_camo_F", "10Rnd_338_Mag", "optic_AMS_khk"],
    ["srifle_DMR_03_khaki_F", "20Rnd_762x51_Mag", "optic_Arco"],
    ["arifle_SPAR_03_khk_F", "20Rnd_762x51_Mag", "optic_ERCO_khk_F"],
    ["srifle_EBR_F", "20Rnd_762x51_Mag", "optic_Hamr"],
    ["srifle_DMR_06_olive_F", "20Rnd_762x51_Mag", "optic_MRCO"],
    ["srifle_DMR_01_F", "10Rnd_762x54_Mag", "optic_MRCO"],
    ["arifle_MXM_F", "30Rnd_65x39_caseless_mag", "optic_AMS_snd"],
    ["srifle_DMR_07_ghex_F", "20Rnd_650x39_Cased_Mag_F", "optic_Arco_ghex_F"],
    ["arifle_AK12_F", "30Rnd_762x39_Mag_F", "optic_Hamr"],
    ["arifle_ARX_ghex_F", "30Rnd_65x39_caseless_green", "optic_Hamr_khk_F"],
    ["arifle_AKM_F", "30Rnd_762x39_Mag_F", ""],
    ["arifle_MX_F", "30Rnd_65x39_caseless_mag", "optic_Arco"],
    ["arifle_CTAR_hex_F", "30Rnd_580x42_Mag_F", "optic_ERCO_khk_F"],
    ["arifle_Katiba_F", "30Rnd_65x39_caseless_green", "optic_MRCO"],
    ["arifle_MSBS65_F", "30Rnd_65x39_caseless_msbs_mag", "optic_Hamr"],
    ["arifle_SPAR_01_khk_F", "30Rnd_556x45_Stanag", "optic_aco_grn"],
    ["arifle_TRG21_F", "30Rnd_556x45_Stanag", "optic_Holosight"],
    ["arifle_Mk20_F", "30Rnd_556x45_Stanag", "optic_Aco"],
    ["arifle_AKS_F", "30Rnd_545x39_Mag_F", ""],
    ["arifle_MXC_F", "30Rnd_65x39_caseless_mag", "optic_Aco"],
    ["arifle_SDAR_F", "30Rnd_556x45_Stanag", ""],
    ["arifle_AK12U_F", "30Rnd_762x39_AK12_Mag_F", "optic_Aco_smg"],
    ["SMG_03_khaki", "50Rnd_570x28_SMG_03", ""],
    ["SMG_05_F", "30Rnd_9x21_Mag_SMG_02", "optic_ACO_grn_smg"],
    ["SMG_02_F", "30Rnd_9x21_Mag_SMG_02", "optic_Holosight_smg"],
    ["hgun_PDW2000_F", "30Rnd_9x21_Mag", "optic_Aco_smg"],
    ["SMG_01_F", "30Rnd_45ACP_Mag_SMG_01", "optic_ACO_grn_smg"],
    ["sgun_HunterShotgun_01_F", "2Rnd_12Gauge_Slug", ""],
    ["sgun_HunterShotgun_01_sawedoff_F", "2Rnd_12Gauge_Pellets", ""],
    ["hgun_Pistol_heavy_02_F", "6Rnd_45ACP_Cylinder", "optic_Yorris"],
    ["hgun_Pistol_heavy_01_F", "11Rnd_45ACP_Mag", "optic_MRD"],
    ["hgun_Rook40_F", "16Rnd_9x21_Mag", ""],
    ["hgun_ACPC2_F", "9Rnd_45ACP_Mag", ""],
    ["hgun_P07_F", "16Rnd_9x21_Mag", ""],
    ["hgun_Pistol_01_F", "10Rnd_9x21_Mag", ""],
    ["HandGrenade", "", ""],
    ["HandGrenade", "", ""]
];
publicVariable "GG_var_weaponList";

GG_fnc_suspendUnit = {
    params ["_unit"];

    [_unit] spawn {
        params ["_unit"];

        waitUntil {
            if !(
                [_unit] call {
                    params ["_unit"];
                    
                    if (isNull _unit) exitWith {true};
                    [_unit, [0, 0, 10]] remoteExecCall ["setPos", -2];
                    _unit setPos [0, 0, 10];

                    if ((_unit distance [0, 0, 0]) > 100) then {
                        false
                    } else {
                        while {simulationEnabled _unit} do {
                            _unit enableSimulationGlobal false;
                        };
                        
                        if (_unit in (entities [["CAManBase"], [], true, false])) then {
                            _sideStr = str (side (group _unit));

                            if ((random 1) < 0.01) then {
                                GG_var_suspendedUnits = GG_var_suspendedUnits - [objNull];
                            };

                            GG_var_suspendedUnits pushBack _unit;
                            GG_var_suspendedUnits = [GG_var_suspendedUnits, [], {_x getVariable "GG_var_unitId"}, "ASCEND"] call BIS_fnc_sortBy;
                        } else {
                            [_unit] remoteExecCall ["deleteVehicle", _unit];
                        };
                        true
                    }
                }
            ) then {
                sleep 1;
                false
            } else {
                true
            }
        };
    };
};

GG_fnc_applyWeapon = {
    params ["_unit"];
    _num = _unit getVariable "GG_var_weaponNum";

    if (_unit isEqualTo player) then {
        call GG_fnc_refreshNextWeapon;
    };

    if (alive _unit) then {
        _zeroing = (_unit currentZeroing [(currentWeapon _unit), (currentMuzzle _unit)]) select 1;
        _fireMode = (weaponState _unit) select 2;
        _unit setUnitLoadout GG_var_loadout;
        (GG_var_weaponList select _num) params ["_weapon", "_magazine", "_scope"];

        if (_weapon isKindOf [_weapon, (configFile >> "CfgWeapons")]) then {
            _unit addMagazines [_magazine, 2];
            _unit addWeaponGlobal _weapon;
            _muzzle = currentMuzzle _unit;
            _unit selectWeapon [_weapon, _muzzle, _fireMode];

            _currentZeroing = _zeroing;

            while {!(_unit setWeaponZeroing [_weapon, _muzzle, _currentZeroing]) && {_currentZeroing > 0}} do {
                _currentZeroing = _currentZeroing - 1;
            };

            _currentZeroing = _zeroing;

            while {!(_unit setWeaponZeroing [_weapon, _muzzle, _currentZeroing]) && {_currentZeroing < 7}} do {
                _currentZeroing = _currentZeroing + 1;
            };

            if (_scope != "") then {
                if (_weapon isKindOf ["Pistol", (configFile >> "CfgWeapons")]) then {
                    _unit addHandgunItem _scope;
                } else {
                    _unit addPrimaryWeaponItem _scope;
                };
            };

            if ((_unit isEqualTo player) && {GG_var_nvGogglesEnabled}) then {
                player action ["nvGoggles", player];
            };
        } else {
            _unit addMagazine _weapon;
        };
    };
};
publicVariable "GG_fnc_applyWeapon";

GG_fnc_HandleDisconnect = {
    params ["_unit", "_id", "_uid", "_name"];    

    if ((name _unit) == _name) then {
        [_unit, _name] spawn {
            params ["_unit", "_name"];

            if ((!GG_var_ended) && {(GG_var_leaderBoard find _name) != -1}) then {
                call GG_fnc_refreshLeaderBoardServer;
            };

            waitUntil {!(isPlayer _unit)};
            _par = objectParent _unit;

            if (isNull _par) then {
                [_unit] remoteExecCall ["deleteVehicle", _unit];

                [_unit] spawn {
                    params ["_unit"];

                    while {!(isNull _unit)} do {
                        sleep 1;
                        deleteVehicle _unit;
                    };
                };
            } else {
                [_par, _unit] remoteExecCall ["deleteVehicleCrew", _par];

                [_par, _unit] spawn {
                    params ["_par", "_unit"];

                    while {!(isNull _unit)} do {
                        sleep 1;
                        _par deleteVehicleCrew _unit;
                    };
                };
            };
        };
    };
};

GG_fnc_getAiNameArr = {
    params ["_unit"];

    _name = [];
    _ss = (name _unit) splitString " ";
    _sss = (_ss select 0) splitString "-";

    if ((count _sss) > 1) then {
        _name = [
            (((_sss select 0) select [0,1]) + ".-" + ((_sss select 1) select [0,1]) + "."),
            ((_ss select 1) + " (AI)")
        ];
    } else {
        _name = [
            (((_ss select 0) select [0,1]) + "."),
            ((_ss select 1) + " (AI)")
        ];
    };

    ([(_name select 0) + " " + (_name select 1)] + _name)
};

GG_fnc_getPicture = {
    getText (configfile >> (
        if (
            ((_this select 0) isKindOf "Car") ||
            ((_this select 0) isKindOf "Helicopter") ||
            ((_this select 0) isKindOf "Tank") ||
            ((_this select 0) isKindOf "StaticWeapon")
        ) then {
            "CfgVehicles"
        } else {
            if ((_this select 0) isKindOf [(_this select 0), (configFile >> "CfgMagazines")]) then {
                "CfgMagazines"
            } else {
                "CfgWeapons"
            }
        }
    ) >> (_this select 0) >> "picture")
};

GG_fnc_entityKilled = {
    if !GG_var_ended then {
        params ["_unit", "_killer", "_instigator"];

        if (_unit isKindOf "CAManBase") then {
            if !(_unit in [_killer, _instigator]) then {
                if (isNull _instigator) then {
                    _instigator = (UAVControl (objectParent _killer)) select 0
                };

                if (isNull _instigator) then {
                    _instigator = _killer
                };

                _instigator addplayerscores [2, 0, 0, 0, 0];
                _instigator addRating ((-3000) - (rating _instigator));

                _instigator setVariable ["GG_var_weaponNum", ((_instigator getVariable "GG_var_weaponNum") + 1), true];
                _unit setVariable ["GG_var_deaths", ((_unit getVariable "GG_var_deaths") + 1), true];
                _uid = getPlayerUID _instigator;

                if (isPlayer _instigator) then {
                    GG_var_unitsStats set [((GG_var_unitsStats apply {_x select 0}) find _uid), [_uid, (_instigator getVariable "GG_var_weaponNum")]];

                    [
                        "gunGame", [
                            (format ["%1/%2", (_instigator getVariable "GG_var_weaponNum"), ((count GG_var_weaponList) - 1)]),
                            "",
                            (format ["Enemy killed, %1 left", (((count GG_var_weaponList) - 1) - (_instigator getVariable "GG_var_weaponNum"))])
                        ]
                    ] remoteExecCall ["bis_fnc_shownotification", _instigator];
                };

                [_instigator] remoteExecCall ["GG_fnc_applyWeapon", _instigator];

                if ((_instigator getVariable "GG_var_weaponNum") == ((count GG_var_weaponList) - 1)) then {
                    GG_var_ended = true;

                    if !(isNil "kf_fnc_DisplayKillInfo") then {
                        call kf_fnc_DisplayKillInfo;
                    };

                    [] spawn {
                        sleep 15;
                        "END1" call BIS_fnc_endMissionServer;
                    };
                };

                call GG_fnc_refreshLeaderBoardServer;
            };
        };
    };
};

GG_fnc_EntityRespawned = {
    params ["_newEntity", "_oldEntity"];

    _newEntity setVariable ["rankOnServer", (_oldEntity getVariable "rankOnServer"), true];
    _newEntity addRating ((-3000) - (rating _newEntity));

    if !(isPlayer _newEntity) then {
        deleteVehicle _oldEntity;
        [_newEntity] call GG_fnc_suspendUnit;
    };
};

GG_fnc_spawnAi = {
    params ["_unit"];

    _startTime = time;
    _safePos = getMarkerPos "GG_var_playZone";
    _playZonePos = _safePos;

    waitUntil {
        _safePos = [_playZonePos, 0, GG_var_maxZoneSize, 3, 0, 0, 0, [], _playZonePos] call BIS_fnc_findSafePos;

        if (isPlayer _unit) exitWith {true};

        (
            if (
                !(surfaceIsWater _safePos) &&
                {_safePos inArea "GG_var_playZone"} &&
                {
                    _safePosAsl = _safePos;
                    _safePosAsl set [2, 0];
                    _safePosAsl = AGLToASL _safePosAsl;
                    _safePosAsl set [2, ((_safePosAsl select 2) + 0.5)];

                    (([objNull, "VIEW"] checkVisibility [_safePosAsl, (_safePosAsl vectorAdd [0, 0, 100])]) != 0)
                }
            ) then {
                if ((time - _startTime) < 5) then {
                    if !(_safePos isEqualTo _playZonePos) then {
                        if (((_unit getVariable "GG_var_lastSpawnPos") distance _safePos) >= (GG_var_maxZoneSize * 0.38)) then {
                            if (
                                (
                                    {
                                        if ((_x distance _safePos) > (GG_var_maxZoneSize / 3)) then {
                                            ([objNull, "VIEW"] checkVisibility [(eyePos _x), (_safePos vectorAdd [0, 0, 1.5])] > 0)
                                        } else {
                                            true
                                        }
                                    } count (((entities [["CAManBase"], [], true, false]) inAreaArray "GG_var_playZone") select {alive _x})
                                ) == 0
                            ) then {
                                true
                            } else {
                                false
                            }
                        } else {
                            false
                        }
                    } else {
                        false
                    }
                } else {
                    true
                }
            } else {
                false
            }
        )
    };

    if (isPlayer _unit) exitWith {};

    _unit setVariable ["GG_var_lastSpawnPos", _safePos];

    _unit setPos _safePos;
    [_unit] call GG_fnc_applyWeapon;
    [_unit, false] call ADG_fnc_allowDamage;
    _unit enableSimulationGlobal true;
    _unit addPlayerScores [0, 0, 0, 0, 0];
    _unit setSkill (_unit getVariable "GG_var_aiSkill");
    (group _unit) setCombatBehaviour "COMBAT";
    _unit setCombatBehaviour "COMBAT";
    (group _unit) setCombatMode "RED";
    _unit setUnitCombatMode "RED";
    _unit enableAI "all";

    [_unit] spawn {
        params ["_unit"];

        sleep 3;

        if (isPlayer _unit) exitWith {};

        if (alive _unit) then {
            [_unit, true] call ADG_fnc_allowDamage;
        };
    };

    _safePosAsl = getPosATL _unit;
    _safePosAsl set [2, 0];
    _safePosAsl = AGLToASL _safePosAsl;
    _safePosAsl set [2, ((_safePosAsl select 2) + 0.5)];

    if (([objNull, "VIEW"] checkVisibility [_safePosAsl, (_safePosAsl vectorAdd [0, 0, 100])]) == 0) exitWith {
        [_unit] call GG_fnc_suspendUnit;
    };

    [_unit] spawn {
        params ["_unit"];

        waitUntil {
            if (isPlayer _unit) exitWith {true};

            _lastPos = getPosATL _unit;
            _currentDestination = getPosATL _unit;

            while {(_currentDestination distance _unit) < (0.38 * GG_var_maxZoneSize)} do {
                _currentDestination = "GG_var_playZone" call BIS_fnc_randomPosTrigger;
            };

            [_unit, _currentDestination] remoteExecCall ["doMove", (groupOwner (group _unit))];

            waitUntil {
                sleep 1;

                if (isPlayer _unit) exitWith {true};

                if (_unit inArea "GG_var_playZone") then {
                    _lastPos = getPosATL _unit;

                    !(alive _unit) || {(_unit distance _currentDestination) < 10}
                } else {
                    _unit setPos _lastPos;

                    true
                }
            };

            if (isPlayer _unit) exitWith {true};

            !(alive _unit)
        };
    };
};

GG_fnc_applyMap = {
    _idx = if (("Map" call BIS_fnc_getParamValue) == 0) then {
        floor (random (count GG_var_mapData))
    } else {
        ("Map" call BIS_fnc_getParamValue) - 1
    };

    (GG_var_mapData select _idx) params ["_coverMapPos", "_coverMapDir", "_coverMapSize", "_playZonePos", "_playZoneSize", "_playZoneDir", "_playZoneShape"];

    GG_var_coverMap setPos _coverMapPos;
    GG_var_coverMap setVariable ["objectArea", [_coverMapSize, _coverMapSize, _coverMapDir, true, 0]];
    [GG_var_coverMap, [], true] call BIS_fnc_moduleCoverMap;

    "GG_var_playZone" setMarkerPos _playZonePos;
    "GG_var_playZone" setMarkerSize _playZoneSize;
    "GG_var_playZone" setMarkerDir _playZoneDir;
    "GG_var_playZone" setMarkerShape _playZoneShape;

    startSize = _playZoneSize;
    GG_var_maxZoneSize = selectMax startSize;
    sizeFactor = ((startSize select 0) * (startSize select 1) / 4560) ^ 0.05;
    zoneStartPos = _playZonePos;

    if (_playZoneShape == "ELLIPSE") then {
        sizeFactor = sizeFactor * pi
    };

    publicVariable "GG_var_maxZoneSize";
};

GG_fnc_refreshLeaderBoardServer = {
    call GG_fnc_setPlayerRanks;

    _maxNum = (count GG_var_weaponList) - 1;
    _players = ([((entities [["CAManBase"], [], true, false]) select {!(isNil {_x getVariable "rankOnServer"})}), [], {_x getVariable "rankOnServer"}, "ASCEND"] call BIS_fnc_sortBy) select [0, 3];
    GG_var_leaderBoard = (
        [
            "<t size='0.6' align='center'>",
            (if GG_var_ended then {"Game ended, winners are:<br/>"} else {""}),
            (
                (
                    _players apply {
                        _num = _x getVariable "GG_var_weaponNum";

                        format [
                            "%1. %2 <img size='0.75' color='#ffffff' image='%3'/> %4/%5",
                            (_x getVariable "rankOnServer"),(name _x), ([(GG_var_weaponList select _num) select 0] call GG_fnc_getPicture), _num, _maxNum
                        ]
                    }
                ) joinString "<br/>"
            ),
            "</t>"
        ] joinString ""
    );

    if hasInterface then {
        [] spawn {
            waitUntil {!(isNil "GG_fnc_refreshLeaderBoardClient")};
            call GG_fnc_refreshLeaderBoardClient;
        };
    };

    publicVariable "GG_var_leaderBoard";
};

GG_fnc_setPlayerRanks = {
    _players = [((entities [["CAManBase"], [], true, false]) select {!(isNil {_x getVariable "GG_var_weaponNum"})}), [], {_x getVariable "GG_var_weaponNum"}, "DESCEND"] call BIS_fnc_sortBy;

    _i = 1;
    for "_i" from 1 to (count _players) do {
        (_players select (_i - 1)) setVariable ["rankOnServer", _i, true];
    };
};

GG_fnc_respawnServer = {
    params ["_unit", "_corpse"];

    deleteVehicle _corpse;
};

GG_fnc_initPlayer = {
    params ["_player"];

    _player addRating ((-3000) - (rating _player));
    _player setVariable ["GG_var_deaths", 0, true];
    _uid = getPlayerUID _player;
    _pos = (GG_var_unitsStats apply {_x select 0}) find _uid;
    _newNum = if (_pos == -1) then {0} else {(GG_var_unitsStats select _pos) select 1};
    _initPlayersNums = (((entities [["CAManBase"], [], true, false]) - [_player]) select {!(isNil {_x getVariable "GG_var_weaponNum"})}) apply {_x getVariable "GG_var_weaponNum"};

    if !(_initPlayersNums isEqualTo []) then {
        if (({_x <= _newNum} count _initPlayersNums) == 0) then {
            _newNum = selectMin _initPlayersNums;
        };
    };

    _player setVariable ["GG_var_weaponNum", _newNum, true];

    if (_pos == -1) then {
        GG_var_unitsStats pushBack [_uid, _newNum];
    };

    if !GG_var_ended then {
        call GG_fnc_refreshLeaderBoardServer;
    };
};